//-----------------------------------------------------------------------
//
//                    Transcriptor de Código Morse
//
//------------------------------------------------------------------------
// Codigo finalizado el día 18/8/23
// Programa desarrollado por Eduardo Bogado
// Universidad Central de Venezuela
// Github user eduardob999
// Algoritmo y Programación
// Profa. Yusneyi Carballo Barrera

Acción Principal
    // Acá se manejan la iniciación y finalización del programa,
    // así como también, se declaran e inicializan las variables
    // compartidas entre procedimientos.

    // IMPORTANTE: Este algoritmo se desarrollo en Pseudocódigo
    // con la final de tener transferibilidad máxima a c++
    // la organización, la elección de funciones y procedimeintos
    // Se hizo con la finalidad de poder transcribirse a c++
    // con facilidad.

    // Declaración de variables
    Entero inp, cantmsg, nerror; 
    string menu0, cond0, cantpal, morsefinal, texto;
    Real totpal;

    cantmsg = 0;
    cantpal = "";
    totpal = 0;
    nerror = 0;
    morsefinal = "";
    texto = "";

    // Inicialización de variables
    menu0 = "SubcadenaUniversidad Central de Venezuela"
    		"SubcadenaFacultad de Ciencias"
            "SubcadenaEscuela de Computación"
            "SubcadenaAlgoritmos y Programación. Sem I-2023."
            "SubcadenaProyecto 1. Código Morse Internacional."
    		"Subcadena"
            "SubcadenaMenú:"
            "Subcadena  1.  Integrantes de Equipo"
            "Subcadena  2.  Transmitir Texto en Código Morse"
            "Subcadena  3.  Convertir Código Morse a Texto"
            "Subcadena  4.  Estadísticas"
            "Subcadena  5.  Salir"
            "Subcadena"
            "SubcadenaPor favor, indique su opción:";
    cond0 = "SubcadenaDebe ingresar un número entero entre 1 y 5. "
            "SubcadenaIngrese su opción nuevamente:";

    // Procesamiento
    Repetir
        
        // En este ciclo se controla la ejecicón y salida del programa
        // la variable inp se emplea como bandera
        InputEntero( menu0 , inp , 1 , 5 , cond0 );
        Seleccion( inp , cantmsg , cantpal , totpal , nerror , morsefinal, texto );
    Hasta ( inp == 5 );
FAcción;

Acción InputEntero( string prompt , Ref Entero in , Entero min , Entero max , string con)    
    // Esta acción maneja las entradas de datos enteros incluyendo la
    // solicitud de datos y la validadción en base a una serie de
    // parámetros entre los que se incluyen los valores máximos y mínimos.
    // Declaración de cadena de entrada.
    string n;

    Escribir( prompt );
    Repetir

        // Ciclo de validación basada en valores máximos y mínimos
        // Solicitud y transformación de datos
        Leer( n ); 
        in = aEntero( n );
        Si ( No ( in >= min Y in <= max ) ) Entonces

            // Solicitud de correción personalizable.
            Escribir( con );
        FSi;
    Hasta ( in >= min Y in <= max );
FAcción;

Acción Seleccion( Ref Entero in , Ref Entero cmt , Ref String cpal, Ref Real tpal , Ref Entero nerr , Ref String morse, Ref String msg )
    // En este procedimiento se maneja el menúi de selección

    // Declaracion de variables
    string txt, dic, ver, ver2, ver3, menu1, cond1, menu2, cond2, menu3, cond3, proces;
    Entero flag;

    // Inicialización de los strings correspondientes a los textos y 
    // condiciones de cada menú.
    dic = "_a_.-_i_.._r_.-._b_-..._j_.---_s_..._c_-.-._k_-.-_t_-_d_-.._l_.-.._"
          "u_..-_e_._m_--_v_...-_è_..-.._n_-._w_.--_f_..-._o_---_x_-..-_g_--._"
          "p_.--._y_-.--_h_...._q_--.-_z_--.._ _/_error_........_1_.----_6_-...."
          "_2_..---_7_--..._3_...--_8_---.._4_....-_9_----._5_....._0_-----";
    ver = "Subcadena¿Desea volver al menu principal?"
           "SubcadenaResponda con Sí o No:";
    ver2 = "Subcadena¿Desea transmitir otro mensaje?"
           "SubcadenaResponda con Sí o No: ";
    ver3 = "Subcadena¿Desea convertir otro mensaje?"
           "SubcadenaResponda con Sí o No:";
    menu1 = "SubcadenaOpción 1 - Integrantes del Equipo."
            "Subcadena"
            "SubcadenaUniversidad Central de Venezuela"
    		"SubcadenaFacultad de Ciencias"
            "SubcadenaEscuela de Computación"
            "SubcadenaAlgoritmos y Programación. Sem I-2023."
            "Subcadena"
            "SubcadenaEduardo Alfonso Bogado Rojas"
            "SubcadenaC.I: v-26.818.715"
            "Subcadena"
            "SubcadenaMenú:"
            "Subcadena"
            "Subcadena  1.  Regresar al menú anterior"
            "Subcadena"
            "SubcadenaPor favor, indique su opción:";
    cond1 = "SubcadenaDebe ingresar al número entero 1. "
            "SubcadenaIndique su opción nuevamente:";
    menu2 = "SubcadenaOpción 2 - Transmitir Texto en Código Morse"
            "Subcadena"
            "SubcadenaEl mensaje no debe incluir más de 30 caracteres."
            "SubcadenaPor favor, introduzca el texto a codificar:";
    cond2 = "SubcadenaEl texto debe tener de 1 a 30 caracteres."
            "SubcadenaEvite el uso de acentos ajenos al idioma español."
            "SubcadenaSe admiten solo caracteres incluidos en la convención ITU-RM.1677-1"
            "SubcadenaPor favor, introduzca nuevamente el texto a codificar:";
    menu3 = "SubcadenaOpción 3 - Convertir Código Morse en Texto Normal"
            "Subcadena"
            "SubcadenaEl mensaje no debe incluir más de 50 caracteres."
            "SubcadenaSe admiten solo caracteres incluidos en la convención ITU-RM.1677-1"
            "SubcadenaPor favor, introduzca la codificación:";
    cond3 = "SubcadenaEl texto debe tener de 1 a 50 caracteres."
            "SubcadenaEl punto, señal corta o dot viene dado por el caracter . ."
            "SubcadenaLa línea, señal larga o dash viene dado por el caracter - ."
            "SubcadenaLas letras deben ir separadas por espacios."
            "SubcadenaLas palabras vienen separadas por el caracter / ."
            "SubcadenaPor favor, introduzca la codificación nuevamente: ";

    // Selección correspoendiente a las opciones del menú.
    Selección 
    in == 1:

        // Opción 1 - Identificación del equipo.
        InputEntero( menu1 , in , 1 , 1 , cond1 );
    in == 2:

        // Opción 2 - Codificación de texto a código morse.
        Repetir

            // Este ciclo permite la reutilización del menú
            // sin neecesidad de volver al menú inicial.
            // El procesamiento y traducción de la entrada es llevada a
            // cabo por la fucnión OutputMorse.
            InputString( menu2, txt, 1, 30, cond2, dic, 1 );
            OutputMorse( txt, dic, cmt, cpal, tpal, proces );
            msg = msg + txt + " ";
            morse = morse + proces + " //";
        Hasta ( Verificador( ver2 ) );
    in == 3:

        // Opción 3 - Decodificación de código Morse a Texto.
        Repetir

            // Este ciclo permite la reutilización del menú
            // sin neecesidad de volver al menú inicial.
            // El procesamiento y traducción de la entrada es llevada a
            // cabo por la acción OutputTexto.
            InputString( menu3 , txt , 1 , 50 , cond3, "_._-_/_ _", 1 );
            OutputTexto( txt , dic , nerr );
        Hasta ( Verificador( ver3 ) );
    in == 4:
        
        // Opción 4 - Estadística.
        Repetir

            // Este ciclo permite la reutilización del menú
            // sin neecesidad de volver al menú inicial.
            // El procesamiento y traducción de la entrada es llevada a
            // cabo por la acción OutputStat.
            Escribir( "Opción 4 - Estadísticas" );
            OutputStats( cmt , cpal , tpal, nerr, morse, msg );
        Hasta ( No (  Verificador( ver ) ));
        
    in == 5:
        
        // Opción 5 - Salida y resumen de actividad.
        // Se reutiliza la acción OutputStat para mostrar
        // el resumen de actividad.
        Escribir( "Opción 5 - Salir. A continuación un resumen de su actividad:" );
        OutputStats( cmt , cpal , tpal, nerr, morse, msg );
        Escribir( "Gracias por usar este programa." );
        in = 5;
    FSelección;
FAcción;

Acción InputString( string prompt , Ref String t , Entero min , Entero max , string con, string d, Entero size )

    // Esta acción manea todas las entradas de dato tipo string.
    // Se incluye solicitud de datos y validación.
    // Este procedmiento se diseño con la finalidad de ser altamente
    // reutilizable.

    // Declaración de variables.
    Entero ind, flag, i;

    // Procesamiento: Se comienzo mostrarndo el mensaje de solicitud
    // El mensaje de solicitud se obtiene como parametro.
    Escribir( prompt );
    Repetir

        // Este ciclo es un ciclo de validación.
        // Inicialización de contadores.
        flag = 0; i = 0;

        // Procesamiento: se comienza solicitando el texto de entrada.
        Leer( t );
        Repetir

            // Este ciclo verifica cada letra de la entrada
            // el kernel es una acción que permite identificar la
            // posición relativa de un caracter en una cadena.
            Kernel( i + 1, ind , t , d , size );
            Si (No ( ( Longitud(t) >= min Y Longitud(t) <= max ) Y ind < Longitud(d) - 1 )) Entonces

                // En este condicional se verifica la longitud de la
                // cadena, los límites son personalizbles
                flag = -1;
            FSi;
            i = i + size + 1;
        Hasta ( i >= Longitud(t) );

        Si ( flag == -1 ) Entonces
            
            // Este condicional muestra un mensaje de corrección de ser necesario.
            Escribir( con );
        FSi;
    Hasta ( flag =/= -1 );
FAcción;

Acción OutputMorse( string t , string d , Ref Entero nm , Ref String np , Ref Real tp , Ref String proc)
  
    // Esta acción procesa las entradas de texto y las convierte en morse.
    
    // Declaración de variables.
    string selec; Entero i, ind, q, palen;

    // Inicialización de variables.
    proc = "";
    palen = 0;

    // Procesamimiento de la caena de entrada "t".
    Para i = 1 Hasta Longitud(t) Hacer

        // Este ciclo verifica cada letra de la entrada
        // se reutiliza el kernel en esta acción
        // el cuantificador determina la cantidad de carcaternes
        // que tendrá cada salida.
        Kernel( i , ind , t , d , 1 );
        Cuantificador( ind , q , t , d );

        Si ( Subcadena( d , ind + 3 , q ) == "/" O i + 1 > Longitud(t) ) Entonces
            
            // Subcadena se implementa como función primitiva 
            // Función Subcadena( String entrada, Entero inicial, Entero Cantidad ):string
            // Este ciclo cuenta la cantidad de palabras y las almacena en "palen".
            palen = palen + 1;
        FSi;
        proc = proc + Subcadena( d, ind + 3 , q ) + " ";
    FPara;

    // Procesamiento de datos intermedios.
    nm = nm + 1;
    np = np + to_string( palen ) + " ";
    tp = tp + palen;

    // Codigo morse codificado:
    Escribir( "La Codificación Morse de " + t + " es:Subcadena" + proc);

FAcción;

Acción Kernel( Entero j, Ref Entero ind , string t , string d , Entero size )

    // El kernel identifica la ubicaciñon relativa de una cadena de caracteres en otra
    // y le asigna un índice entero.

    // Inicialización del contador principal.
    ind = 0;

    // Procesamiento y generación del índice.
    Repetir
        
        // Este ciclo encuentra el valor del índice según las coincidencias dadas.
        ind = ind + 1 ;
    Hasta (  "_" + Subcadena( t , j , size ) + "_" == Subcadena( d , ind , size + 2 ) O  ind + size > Longitud(d)  );

FAcción;

Acción Cuantificador( Entero ind , Ref Entero q , string t , string d )

    // Esta acción mide el tamaño de una subcadena en un string
    // este procedimiento es específcio de la acción OutputMorse.

    // Inicialición del contador principal.
    q = 0;
    Repetir
        
        // Este ciclo encuentra el valor del tamaño de la subcadena según las especificaciones.
        q = q + 1;
    Hasta ( ind + q + 2 > Longitud(d) O ( Subcadena( d , ind + q + 2 , 1 ) =/= "." Y Subcadena( d, ind + q + 2 , 1 ) =/= "-"  Y Subcadena( d , ind + q + 2 , 1 ) =/= "/" ) );
    q = q - 1;
FAcción;
         
Función Verificador( string p )
    // Función reutlizable empleada para determinar la condición de salida
    // de un ciclo con finalidad de reutilizar una serie de
    // procedimeintos.

    // Declaraciónes de variables.
    string cond, ans, acc, rej; Entero sind, nind, flag;

    // Inicialización de variables.
    acc = "_SI_Si_sI_si_Sí_sÍ_SÍ_sí_";
    rej = "_No_nO_NO_no_";
    cond = "SubcadenaDebe responder con Sí o No"
           "SubcadenaIntroduzca su respuesta nuevamente: ";

    // Solicitud de entrada y procesamiento.
    Repetir

        //Este es un ciclo de validación.
        InputString( p , ans , 2 , 2 , cond, acc + rej, 2);
        Kernel( 1 , sind , ans , acc , 2);
        Kernel( 1 , nind , ans , rej , 2);
        Si ( sind < Longitud(acc) - 1 ) Entonces

            // Este condicional verifca el resultado positivo.  
            retornar Falso;
            Sino
                Si ( nind < Longitud(rej) - 1 ) Entonces

                    // Este condicional verifca el resultado negativo.
                    retornar Verdadero;
                    Sino

                    // En caso no definido se solicitan los datos nuevamente.
                    Escribir(cond);
                    flag = -1;
                FSi;
        FSi;
    Hasta ( flag =/= -1 );
FAcción;

Acción OutputTexto( string t , string d , Ref Entero ne )

    // Esta acción procesa las entradas de codigo morse y las convierte en
    // cadenas de caracteres con texto.
    
    // Declaración de variables.
    string prompt, cond, selec, proc; Entero i, size, ind, q, flag, errs, errind;

    // Inicialización de contadores indices y datos intermedios:
    proc = "";
    i = 0;

    // Procesa y muestra la salida.
    Repetir
        
        // Este ciclo realiza la traducción del código morse al texto.
        
        // Inicialización de flags y datos intermedios.
        // Los datos intermedios son datos que se utilizan para
        // operaciones matemáticas no se muestran al usuario sin ser
        // procesados.
        flag = 1;
        errs = 0;

        // Llamada a acciones que identifican las variables
        // TokenSize es una acción muy similar al cuantificador.
        TokenSize( i + 1 , size , t );
        Kernel( i + 1 , ind , t , d , size);

        // Verificación y corrección de errores.
        Si ( i + size + 1 < Longitud(t) ) Entonces

            // Este ciclo verifica la existencia de errores y mediante el uso de
            // flags omite a la entrada errada
            TokenSize( i + size + 2 , errs , t );
            Kernel( i + size + 2 , errind , t , d , errs);
            Si ( Subcadena( d, errind - ( 1 ) , 10 ) == "_........_" ) Entonces
                
                // Flags que permiten la eliminación y conteo de errores
                // este condicional viene dado por el carcater "error"
                flag = -1;
                i = i + errs + 1;
                ne = ne + 1;
            FSi;
        FSi;
        Si ( flag == 1 ) Entonces
            
            // Se porudce la salida si no hubo ningún error
            proc = proc + Subcadena( d, ind - 1 , 1 );
        FSi;
        i = i + size + 1;  
    Hasta ( i >= Longitud(t) );

    // Salida del texto decodificado
    Escribir("El texto decodificado es: " + proc );
FAcción;

Acción TokenSize( Entero j , Ref Entero s , string t )

    // Esta acción mide el tamaño de una subcadena en un string
    // su función es muy similar a la del cuantificador.

    // Inicialición del contador principal.
    s = 0;
    Repetir
        
        // Incremento del contador según las longitudes de las subacaenas
        // y los carcateres especificados.
        s = s + 1;
    Hasta ( Subcadena( t, j + s , 1 ) == " " O j + s > Longitud(t) );
FAcción;

Acción OutputStats( Entero n , string cp , Real tp , Entero ne , string m , string t )

    // Salida de datos estadísticos procesados a partir de datos intermedios.
    Escribir( "Cantidad de mensajes transmitidos: " + n
              + "Cantidad de palabras en cada mensaje (por separado): " 
              + cp + "Porcentaje de palabras por mensaje: "             );

         Si ( n =/= 0 ) Entonces

            //Se verifica un deniminador no nulo
            Escribir( ( tp / n ) * 100 + " %" );
             Sino

            // Mensaje por defecto.
            Escribir( "No se ha transmitido ningun mensaje." );
         FSi;
    Escribir( "Cantidad de errores transmitidos: " + ne
              + "Cadena(s) con el (los) mensaje(s) transmitido(s) en texto: " + t
              + "Cadena(s) con el (los) mensaje(s) transmitido(s) en morse: " + m );
FAcción;
            